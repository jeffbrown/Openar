<h3>4 Developing Oplix: A Quick Tour</h3>

<p>This section describes the overview of developing oplix. We create an oplix called <code>cc</code>, an oplix port of <a href="http://clojure.org/jvm_hosted/">a Celsius converter</a>.</p>

<h4>4.1 The <code>create-oplix</code> Function</h4>

<p>As explained in the section "2 Getting Started", an oplix is an aggregate of libs providing definitions for particular Openar application. Each oplix has a main lib whose namespace name consists of the prefix <code>"oplix."</code> and the oplix name. For example, the oplix <code>openar</code> has the main lib <code>oplix.openar</code>.</p>

<p>You create the main lib of the oplix <code>cc</code> using the <code>create-oplix</code> function. Given an oplix name the <code>create-oplix</code> function writes the main lib file filled with startup code. Open the REPL and call the function like this:</p>

<pre><code>    (create-oplix 'cc)
</code></pre>

<p>Notice that the Openar now shows the oplix name <code>cc</code> in the left pane. You can open <code>cc</code> right away.</p>

<p>"Opening an oplix" creates an instance of the oplix. An oplix instance is a map that represents an oplix as executable entity and contains values required to execute the oplix functions. Each oplix instance has a unique name, normally generated by Openar automatically, and a JFrame that is used as the main window of the oplix instance.</p>

<p>Double-click <code>cc</code> in the Openar to open it. An instance of <code>cc</code> is created and the main window opens. The instance name is displayed in the window title.</p>

<p><img src="../res/ss-dev-oplix1.png" alt="Opening cc" title="Opening cc" /></p>

<p>Before we move on and take a look at the main lib file, let's see some Openar functions that you use to control oplix instance.</p>

<h4>4.2 Opening, Closing, Saving, and Deleting Oplix Instance</h4>

<p>You can open oplix at the REPL using the <code>open-oplix</code> function like this:</p>

<pre><code>    (open-oplix 'cc)
</code></pre>

<p>This does the same thing as you double-click <code>cc</code> in the Openar. The oplix instance name is auto-generated by Openar using a naming convention in this format.</p>

<pre><code>*Oplix-name-with-initial-capital-letter*[Optinal-number-starting-from-1]
</code></pre>

<p>For examle, the very first instance of <code>cc</code> will be named "Cc", the second instance is "Cc1", the third instance is "Cc2", and so on.</p>

<p>You can also open oplix with instance name you like. Specify the instance name following an oplix name like this:</p>

<pre><code>    (open-oplix 'cc "Celsius Converter")
</code></pre>

<h4>Bear in mind, though, that Openar identifies oplix instance by instance name and instance name only. So each oplix instance name should be unique throughout all oplix instances. This means, for example, you cannot open the oplix cc with instance name "Celsius Converter" when another oplix instance named "Celsius Converter" is running and it doesn't matter whether the running instance is the one of cc or any other oplix.</h4>

<p><img src="../res/ss-dev-oplix2.png" alt="Opening cc with name" title="Opening cc with name" /></p>

<p>Likewise, you can close oplix instance using the <code>close-oplix</code> function with oplix instance name like this:</p>

<pre><code>    (close-oplix "Celsius Converter")
</code></pre>

<p>When closing oplix instance, Openar gives a chance to save instance data to the oplix. Openar also saves the JFrame of oplix instance and loads it back when the oplix with the same instance name is opened. So, when you open "Celsius Converter" again, the window will open at the same location where it was there last time because the window location is a part of saved JFrame data. When oplix instance is running, you can cause the save manually using the <code>save-oplix</code> function. Like the <code>close-oplix</code> function, call the <code>save-oplix</code> function with oplix instance name.</p>

<p>OK, let's delete the oplix instance "Celsius Converter". "Deleting oplix instance" means 1) if the oplix instance is running, close it, and 2) remove the saved instance file(s) if any. Note that it doesn't mean to delete the oplix (the oplix lib files). You use the <code>delete-oplix</code> function to delete oplix instance. The followings open "Celsius Converter" and then delete it.</p>

<pre><code>    (open-oplix 'cc "Celsius Converter")
    (delete-oplix "Celsius Converter")
</code></pre>

<p>After working with various oplixes, you would have bunch of saved oplix instances and some of them may become no longer necessary. You can use the <code>delete-oplix</code> function to get rid of them too. Suppose you have just closed "Celsius Converter". To delete it call the <code>delete-oplix</code> function with oplix and instance names like this:</p>

<pre><code>    (delete-oplix 'cc "Celsius Converter")
</code></pre>

<p>To get the names of saved oplix instances, call the <code>get-saved-oplix-names</code> function with oplix name. For example:</p>

<pre><code>    (get-saved-oplix-names 'cc)
</code></pre>

<h4>4.3 The Main Lib File and *oplix*</h4>

<p>From the Openar you can not only open oplix but also open the main lib file of the oplix in Ced. To open it, hold down the META key and double-click oplix name.</p>

<p>Open the main lib file of <code>cc</code>. As you can see, the initial, auto-generated startup code is very simple.</p>

<pre><code>    1: (ns ^{:oplix true}
    2:   oplix.cc
    3:   (:use [openar config core event log oplix ui utils]))
    4:
    5: (defn opened
    6:   [event]
    7:   (set-oplix-visible))
</code></pre>

<p>Let's take a look at the code line by line.</p>

<p>Line 1: The <code>ns</code> macro creates the namespace of the oplix. Notice that metadata signifying it is an oplix namespace is attached to the namespace var. </p>

<p>Line 2: The namespace name of this oplix</p>

<p>Line 3: Refer to the Openar libs you would require often.</p>

<p>Line 5: When opening oplix, Openar notifies open events to the oplix through the event handling functions defined in the oplix. The <code>opened</code> function is one of them. Openar calls the function when oplix instance is initialized and the JFrame of the instance is ready to become visible.</p>

<p>Line 6: The <code>opened</code> function takes one argument, which conveys various event information.</p>

<p>Line 7: Whenever Openar calls the event handling functions, the current oplix instance is set to the <em><code>*oplix*</code></em> var. The `set-oplix-visible function is an Openar function that refers to *oplix* by default and makes its JFrame visible.</p>

<h4>4.4 Making Changes</h4>

<p>Let's start porting the Celsius Converter code into the oplix <code>cc</code>. First, import the necessary Swing classes. You don't need to import JFrame because it's available in the main lib already (but it makes no harm to import it). The <code>ns</code> macro lines should look like below:</p>

<pre><code>    (ns ^{:oplix true}
      oplix.cc
      (:use [openar config core event log oplix ui utils])
      (import (javax.swing JLabel JTextField JButton)
              (java.awt.event ActionListener)
              (java.awt GridLayout)))
</code></pre>

<p>Now let's take a look at the class documentation of, say, <code>GridLayout</code> to see what it is. Put the caret on or next to the string "GridLayout" and press F1. The documentation page will open in a browser (if not, refer to the API-browser documentation and check your network connection).</p>

<p>In the original Celsius Converter code, a JFrame is created in the <code>celsius</code> function. But it is not necessary in oplix code because Openar creates it for oplix. The <code>celsius</code> function of <code>cc</code>, thus, looks slightly different.</p>

<pre><code>    (defn celsius
      [frame]
      (let [temp-text (JTextField.)
            celsius-label (JLabel. "Celsius")
            convert-button (JButton. "Convert")
            fahrenheit-label (JLabel. "Fahrenheit")]
        (.addActionListener convert-button
          (proxy [ActionListener] []
            (actionPerformed [evt]
              (let [c (Double/parseDouble (.getText temp-text))]
                (.setText fahrenheit-label
                   (str (+ 32 (* 1.8 c)) " Fahrenheit"))))))
        (doto frame
          (.setLayout (GridLayout. 2 2 3 3))
          (.add temp-text)
          (.add celsius-label)
          (.add convert-button)
          (.add fahrenheit-label)
          (.setSize 300 80))))
</code></pre>

<p>This function takes JFrame as argument and returns without making it visible, which is done by the <code>set-oplix-visible</code> function later. So the only remaining thing to do is to get the JFrame of the current oplix instance and call this function in the <code>opened</code> function.</p>

<p>To get the JFrame of the current oplix instance you call the <code>oplix-frame</code> function. Like the <code>set-oplix-visible</code> function, it refers to *oplix* by default and returns the JFrame. Make changes to the <code>opened</code> function to get it look like this:</p>

<pre><code>    (defn opened
      [event]
      (celsius (oplix-frame))
      (set-oplix-visible))
</code></pre>

<p>The port now looks completed. Let's see whether it really works. Press META+S to save the file and then press F2 to load (and compile) it. Nothing should happen if there is no load/compile error.</p>

<p>Let's make an "error" and see what happens if it was there. Remove the slash character '/' of "Double/parseDouble" in the <code>celsius</code> function, save the file and then press F2. This time an Exceptor window with an error message opens in the bottom-right corner of the display. The error message is pointing out the cause, the file name, and the line number. Bring the Ced window editing the file to the front. Make sure to click in the window title, not in the text pane, so that you can see the caret is set at the beginning of the error line. Close the Exceptor window, fix the error and then load the file using F2. Again, you should see no Exceptor window, which means no load/compiler error.</p>

<p>Let's make one final touch; select the whole lines of the <code>celsius</code> function and press TAB to reindent the lines. You can even select the whole lines in the file by pressing META+A and reindent them all, if that's what you really want to do.</p>

<p>When the file currently editing is an oplix lib file, Ced can figure out the oplix name and open the oplix by pressing F5. Do it now and play with your first oplix application!</p>

<p><img src="../res/ss-dev-oplix3.png" alt="Making changes" title="Making changes" /></p>

<h4>4.5 The Frame.xml File and The event-response-donot-save Macro</h4>

<p>The second time and after when you open <code>cc</code>, you will notice there is something wrong; the UI components get duplicated every time you open <code>cc</code>. </p>

<p><img src="../res/ss-dev-oplix4.png" alt="cc with bug" title="cc with bug" /></p>

<p>To understand what is going on, first delete all instances of <code>cc</code> using the <code>delete-oplix</code> function. Then open <code>cc</code> from the Openar (so the instance has the name "Cc") and close it right away. Now you have one saved instance. In this case only the JFrame of "Cc" is saved. It is saved to the file <code>frame.xml</code> in an XML format. Open the file using the <code>browse-frame-xml</code> function and take a look at the content. To do that, call the function at the REPL like this:</p>

<pre><code>    (browse-frame-xml 'cc 'Cc)
</code></pre>

<p>The <code>frame.xml</code> file will be opened in a browser (or an application associated with the <code>xml</code> extension. On my Mac OS X, for example, <code>Dashcode</code>).</p>

<p><img src="../res/ss-dev-oplix5.png" alt="Browsing frame.xml" title="Browsing frame.xml" /></p>

<p>You can also dump the content at the REPL by calling the <code>cat-frame-xml</code> function with the same arguments. In the <code>frame.xml</code> file you can see the UI components created by the <code>celsius</code> function in a serialized form and they all look reasonable; e.g. there are one JTextField for input and two JLabels for displaying the strings "Celsius" and "Fahrenheit". Once again open <code>cc</code>, close it, and then take a look at the content of the XML file. You will now see those UI components are duplicated as you saw them in the window.</p>

<p>That is because, as explained in the section "4.2 Opening, Closing, Saving, and Deleting Oplix Instance" above, Openar loads the saved JFrame back when opening "Cc" the second time and after. And the <code>opened</code> function is called after loading the saved JFrame so that the same UI components get added in addition to the persisting ones. Hence you see duplicates. There are three ways to solve this issue.</p>

<ol>
<li>Don't save instance. Recreate window content every time when opening.</li>
<li>Don't add the UI components if persisting ones exist.</li>
<li>Add them only when JFrame is created at the very first time.</li>
</ol>

<p>Solution #2 and #3 require somewhat advanced knowledges, like how to reinstall ActionLister created by the <code>proxy</code> function and suppressing it from persisting in the <code>frame.xml</code> file. So we don't pursue them here.</p>

<p>Solution #1 is very easy to implement; just add the event handling function below.</p>

<pre><code>    (defn saving
      [event]
      (event-response-donot-save))
</code></pre>

<p>Openar calls the <code>saving</code> function when it's time to save instance data. In return you can tell Openar not to save JFrame by returning the value of the <code>event-response-donot-save</code> macro. Delete all saved instances before you open <code>cc</code> with the new function.</p>

<h4>4.6 The frame-created and frame-loaded Functions</h4>

<p>The <code>frame-created</code> and <code>frame-loaded</code> functions are the event handling functions you define in the main lib of oplix. Openar calls the <code>frame-created</code> function when JFrame is created for oplix instance. Likewise, Openar calls the <code>frame-loaded</code> function when saved JFrame is loaded.</p>

<p>Let's clean up the <code>cc</code> code using the <code>frame-created</code> function and modify the <code>celsius</code> function to return conversion result with unit name in vector.</p>

<pre><code>    (ns ^{:oplix true}
      oplix.cc
      (:use [openar config core event log oplix ui utils])
      (import (javax.swing JLabel JTextField JButton)
              (java.awt.event ActionListener)
              (java.awt GridLayout)))

    (defn celsius
      [c]
      [(+ 32 (* 1.8 c)) "Fahrenheit"])

    (defn frame-created
      [event]
      (let [frame (oplix-frame)
            temp-text (JTextField.)
            celsius-label (JLabel. "Celsius")
            convert-button (JButton. "Convert")
            fahrenheit-label (JLabel. "Fahrenheit")]
        (.addActionListener convert-button
                            (proxy [ActionListener] []
                              (actionPerformed [evt]
                                (let [c (Double/parseDouble (.getText temp-text))]
                                  (.setText fahrenheit-label
                                            (apply print-str (celsius c)))))))
        (doto frame
          (.setLayout (GridLayout. 2 2 3 3))
          (.add temp-text)
          (.add celsius-label)
          (.add convert-button)
          (.add fahrenheit-label)
          (.setSize 300 80))))

    (defn opened
      [event]
      (set-oplix-visible))

    (defn saving
      [event]
      (event-response-donot-save))
</code></pre>

<h4>4.7 Interacting with Oplix</h4>

<p>You can inspect oplix instance at the REPL interactively. Open "Cc" and call the <code>repl</code> function with the instance name like this:</p>

<pre><code>(repl Cc)
</code></pre>

<p>Note that you don't need to quote the name. A REPL window opens and the command prompt indicates that you are in the <code>oplix.cc</code> namespace. Also *oplix* is set to the instance value. Just type <code>*oplix*</code> at the REPL and take a look at the key-values. There are the Openar functions that refer to *oplix* by default and return a value corresponding to the function name as key. For example, the Openar functions <code>oplix-on</code>, <code>oplix-name</code>, and <code>oplix-frame</code> return oplix name, oplix instance name, and JFrame of oplix instance respectively. Those functions also accept an oplix instance value as argument. Try these at the REPL and you should get the same results.</p>

<pre><code>    (oplix-name)
    (oplix-name *oplix*)
    (oplix-name (get-oplix 'Cc))
</code></pre>

<p><img src="../res/ss-dev-oplix6.png" alt="Interacting with olix" title="Interacting with oplix" /></p>

<p>You can get the JFrame of the current oplix instance using the <code>oplix-frame</code> function and make changes to the window directly at the REPL. First, get the JFrame.</p>

<pre><code>    (oplix-frame)
</code></pre>

<p>Then get the Container in the JFrame:</p>

<pre><code>    (.getContentPane *1)
</code></pre>

<p>Then the the JTextField added as the first component to the container:</p>

<pre><code>    (.getComponent *1 0)
</code></pre>

<p>Finally set a new Celsius value to the JTextField.</p>

<pre><code>    (.setText *1 "50")
</code></pre>

<p>Or, use this one liner that runs the whole steps above.</p>

<pre><code>    (-&gt; (oplix-frame) (.getContentPane) (.getComponent 0) (.setText "50"))
</code></pre>

<p>One important rule of Swing is that Swing components can be accessed by only one thread at a time, and normally the thread is the event dispatch thread (the EDT for short). In accordance with the rule you can invoke the above one linear in the EDT using the <code>invoke-later</code> function.</p>

<pre><code>    (invoke-later #(-&gt; (oplix-frame) (.getContentPane) (.getComponent 0) (.setText "50")))
</code></pre>

<p>Notice that *oplix* is available in the EDT too because the <code>invoke-later</code> function sets it up so that the <code>oplix-frame</code> function still works.</p>

<p>Now let's change "Cc" to calculate Fahrenheit to Celsius instead of Celsius to Fahrenheit. First, change the label "Celsius" to "Fahrenheit".</p>

<pre><code>    (-&gt; (oplix-frame) (.getContentPane) (.getComponent 1) (.setText "Fahrenheit"))
</code></pre>

<p>Then redefine the <code>celsius</code> function to convert Fahrenheit to Celsius. You can do it at the REPL as well, but let's do in Ced. Back to the Ced window and change the <code>celsius</code> function like this:</p>

<pre><code>    (defn celsius
      [c]
      [(* (- c 32) 5/9) "Celsius"])
</code></pre>

<p>Press META+S then F2 to load the lib file and update the <code>celsius</code> function. Bring the "Cc" window to the front. If the window still has "50" in the JTextField as it was set above, you'll see "10.0 Celsius" when you click the <code>Convert</code> button.</p>

<p>You can modify running instance interactively and try out new code incrementally this way.</p>

<p><img src="../res/ss-dev-oplix7.png" alt="Interacting with olix #2" title="Interacting with oplix #2" /></p>

<h4>4.8 The purge-oplix Function</h4>

<p>Lastly, when you no longer need an oplix and want to remove not only its instances but also the oplix itself, use the <code>purge-oplix</code> function with oplix name. Call the function, for example, to purge <code>cc</code> like this:</p>

<pre><code>    (purge-oplix 'cc)
</code></pre>

<p>In case you want to retrieve trashed oplix files, look into the directories <code>.openar/trash/src/oplix/</code><em>oplix-name</em> for lib files and <code>.openar/trash/dop/oplix/</code><em>oplix-name</em> for saved instance files.</p>

<h4>Appendix A: Per-oplix ClassLoader</h4>

<p>Each oplix instance has a ClassLoader that is used to locate oplix specific classes. By convention oplix specific class and jar files for oplix <em>X</em> are saved in the directory <code>src/oplix/</code><em>X</em><code>/jvm</code>. The ClassLoader is also used to locate aot-compiled classes of oplix <em>X</em> and they are saved in the directory <code>.openar/classes/oplix/</code><em>X</em>. The parent ClassLoader of per-oplix ClassLoader locates libs and classes global to Openar. So the ClassLoader stack of oplix <em>X</em> should look like below:</p>

<blockquote>
  <p><em>Path-to-the-Openar-directory</em><code>/src/oplix/</code><em>X</em><code>/jvm/</code> <br />
  <em>Path-to-the-Openar-directory</em><code>/.openar/classes/</code> <br />
  <em>Path-to-the-Openar-directory</em><code>/src/</code> <br />
  <em>Path-to-the-Openar-directory</em><code>/lib/clojure.jar</code> <br />
  <em>Path-to-the-Openar-directory</em><code>/lib/clojure-contrib.jar</code> <br />
  --- more Openar lib .jar lines here --- <br />
  --- Java system .jar lines here ---  </p>
</blockquote>

<p>You can print actual ClassLoader stack using the <code>print-cl</code> function at the REPL.</p>

<p>So, for example, if you want to use <em>Y.jar</em> for oplix <em>X</em>, put the jar file in the directory <code>src/oplix/X/jvm</code>. Then you can refer to the class <em>Z</em> in the package <em>Y</em> like "Y.Z".</p>